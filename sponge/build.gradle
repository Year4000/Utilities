/*
 * Copyright 2016 Year4000. All Rights Reserved.
 */
buildscript {
    repositories {
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }
    dependencies {
        classpath 'gradle.plugin.org.spongepowered:SpongeGradle:0.5.1'
    }
}

plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '4.0.4'
}

apply plugin: 'org.spongepowered.plugin'
apply from: utilities.file('gradle/utilities.gradle')
apply from: utilities.file('gradle/src/main/groovy/net/year4000/utilities/gradle/sponge.gradle')

compileJava.options.encoding = 'UTF-8'

spongestarter {
    deleteJar false
    rootProject 'sponge'
}

jar {
    manifest {
        attributes 'Implementation-Title': utilities.name
        attributes 'Implementation-Version': utilities.version
    }
}

assemble.dependsOn shadowJar {
    baseName = utilities.name.toLowerCase()
    version = utilities.version
    configurations += [project.configurations.compile]
    configurations += [project.configurations.shadow]

    exclude 'overview.html'
    exclude 'overviewj.html'
    exclude 'NOTICE'
    exclude 'LICENSE'
}

dependencies {
    compile utilities.project('core')
    compile utilities.project('core-redis')
    compile utilities.project('core-ducktape')
    compile utilities.project('core-ducktape:loaders-groovy')
    compile 'io.netty:netty-all:4.1.5.Final'
    compileOnly 'org.spongepowered:spongeapi:7.1.0-SNAPSHOT'
    testCompile 'org.spongepowered:spongeapi:7.1.0-SNAPSHOT'
}

idea {
    module {
        sourceDirs += file("src/debug/java")
    }
}

// If you run the command with the DEBUG env set it will add the debug files in the classpath
if (System.getenv('DEBUG') != null) {
  sourceSets.main {
    java.srcDir "${projectDir}/src/debug/java/"
    resources.srcDir "${projectDir}/src/debug/resources/"
  }
}
