/*
 * Copyright 2016 Year4000. All Rights Reserved.
 */

import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '1.2.3'
}

apply from: utilities.file('gradle/utilities.gradle')

processResources {
    from('src/main/resources') {
        include 'plugin.yml'
        filter(ReplaceTokens, tokens: [version: utilities.version, git_hash: git()])
    }
}

build.dependsOn shadowJar {
    baseName = utilities.name.toLowerCase()
    version = utilities.version

    dependencies {
        include dependency('redis.clients:jedis:.*')
        include dependency('com.comphenix.packetwrapper:PacketWrapper:.*')
        include dependency('com.sk89q:command-framework-bukkit:.*')
        include dependency('net.year4000.utilities:.*')
    }
}

repositories {
    mavenCentral()
    maven {
        name = 'oc-tc'
        url = 'https://repo.oc.tc/content/repositories/public'
    }
    maven {
        name = 'spigot'
        url = 'https://hub.spigotmc.org/nexus/content/groups/public/'
    }
    maven {
        name = 'dmulloy2'
        url = 'http://repo.dmulloy2.net/content/groups/public/'
    }
}

dependencies {
    compile utilities.project('core')
    compile utilities.project('core-redis')
    compile utilities.project('core-minecraft')
    compile utilities.project('core-locale')
    compile utilities.project('core-sdk')
    compile utilities.project('core-scheduler')
    compile 'org.spigotmc:spigot-api:1.8.7-R0.1-SNAPSHOT'
    compile ('com.comphenix.protocol:ProtocolLib:3.6.5-SNAPSHOT') {
        exclude module: 'bukkit'
    }
    compile ('com.comphenix.packetwrapper:PacketWrapper:1.8.7-R0.1-SNAPSHOT') {
        exclude module: 'bukkit'
        exclude module: 'spigot'
    }
    compile ('com.sk89q:command-framework-bukkit:0.5-SNAPSHOT') {
        exclude module: 'spigot'
        exclude module: 'bukkit'
    }
}
